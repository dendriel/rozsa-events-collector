plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    testImplementation 'org.springframework.boot:spring-boot-starter-web'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/dendriel/rozsa-events-collector"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    repositories {
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Rozsa Events Collector'
                description = 'A spring-based java library that easies the collection and transmission of events in execution flows.'
                url = 'https://github.com/dendriel/rozsa-events-collector'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'dendriel'
                        name = 'Vitor Rozsa'
                        email = 'vitor.rozsa@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dendriel/rozsa-events-collector.git'
                    developerConnection = 'scm:git:git@github.com:dendriel/rozsa-events-collector.git'
                    url = 'https://github.com/dendriel/rozsa-events-collector'
                }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
    // avoid the "-plain" suffix that may cause compatibilities issues with maven
    archiveClassifier = ''

    dependsOn 'javadoc'

    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}